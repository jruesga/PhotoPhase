/*
 * Copyright (C) 2015 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

def libraries = rootProject.ext.libraries

android {
    flavorDimensions 'app'

    defaultConfig {
        versionCode 1036
        versionName "2.7.0"

        applicationId 'com.ruesga.android.wallpapers.photophase'

        return void
    }

    signingConfigs {
        release {
            if (rootProject.file('release.properties').exists()) {
                Properties releaseProps = new Properties()
                releaseProps.load(new FileInputStream(rootProject.file('release.properties')))

                Properties ksProps = new Properties()
                ksProps.load(new FileInputStream(new File((String) releaseProps['signing.config'])))
                storeFile file(ksProps['keystore'])
                keyAlias ksProps['alias']
                storePassword ksProps['storePass']
                keyPassword ksProps['pass']
                storeType ksProps['storeType']
            }

            return void
        }
    }

    buildTypes {
        debug {
            debuggable true

            return void
        }
        release {
            debuggable false

            zipAlignEnabled true
            shrinkResources false
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')

            signingConfig signingConfigs.release

            return void
        }
    }

    productFlavors {
        gms {
            dimension 'app'
            buildConfigField "String", "CAST_IMPL", "\"com.ruesga.android.wallpapers.photophase.cast.GmsCastTaskManager\""
            dependencies {
                implementation project(':gms')
            }
        }
        floss {
            dimension 'app'
            buildConfigField "String", "CAST_IMPL", "\"com.ruesga.android.wallpapers.photophase.cast.FlossCastTaskManager\""
            dependencies {
                implementation project(':floss')
            }
        }
    }

    lintOptions {
        disable 'MissingTranslation', 'ManifestResource'
    }

    return void
}

dependencies {
    implementation project(':native')
    implementation "com.android.support:design:${libraries.support}"
    implementation "com.android.support:cardview-v7:${libraries.support}"
    implementation "com.android.support:exifinterface:${libraries.support}"
    implementation "com.github.danielnilsson9:color-picker-view:${libraries.colorpickerview}"
    implementation ("org.adw.library:discrete-seekbar:${libraries.discrete_seekbar}") { transitive = false }
    implementation ("com.github.chrisbanes:PhotoView:${libraries.photoview}") { transitive = false }
    implementation "com.squareup.okhttp3:okhttp:${libraries.okhttp3}"
    implementation "com.squareup.okio:okio:${libraries.okio}"
    implementation "com.mikepenz:aboutlibraries:${libraries.aboutlibraries}"

    // Chromecast
    implementation ("com.github.jruesga:chromecast-java-api-v2:${libraries.chromecast}") { transitive = false }
    implementation ("com.google.protobuf:protobuf-java:${libraries.protobuf}") { transitive = false }
    implementation ("org.codehaus.jackson:jackson-core-asl:${libraries.jackson}") { transitive = false }
    implementation ("org.codehaus.jackson:jackson-mapper-asl:${libraries.jackson}") { transitive = false }
    implementation "org.nanohttpd:nanohttpd:${libraries.nanohttpd}"
}

apply plugin: 'com.getkeepsafe.dexcount'
