/*
 * Copyright (C) 2015 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    libraries = [
            // https://developer.android.com/topic/libraries/support-library/revisions.html
            support                     : '27.1.1',
            // https://firebase.google.com/support/release-notes/android
            gms                         : '11.8.0',
            okhttp3                     : '3.10.0',
            okio                        : '1.14.1',
            colorpickerview             : '1.4.0',
            discrete_seekbar            : '1.0.1',
            photoview                   : '2.1.3',
            aboutlibraries              : '6.0.8',
            chromecast                  : 'fde403557d',
            protobuf                    : '2.6.1',
            jackson                     : '1.9.13',
            nanohttpd                   : '2.3.1'
    ]

    testLibraries = [
    ]

    sdkVersions = [
            compileSdkVersion           : 27,
            // https://developer.android.com/studio/releases/build-tools.html
            buildToolsVersion           : '27.0.3',
            minSdkVersion               : 15,
            targetSdkVersion            : 27
    ]
}

allprojects {
    buildscript {
        repositories {
            google()
            jcenter()
        }
        dependencies {
            // https://developer.android.com/studio/releases/gradle-plugin.html#revisions
            classpath 'com.android.tools.build:gradle:3.1.2'
            classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'
        }
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.plugins.hasPlugin('android') || project.plugins.hasPlugin('android-library')) {
            android {
                compileSdkVersion rootProject.ext.sdkVersions.compileSdkVersion
                buildToolsVersion rootProject.ext.sdkVersions.buildToolsVersion

                defaultConfig {
                    minSdkVersion rootProject.ext.sdkVersions.minSdkVersion
                    targetSdkVersion rootProject.ext.sdkVersions.targetSdkVersion

                    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_1_7
                    targetCompatibility = JavaVersion.VERSION_1_7
                }

                packagingOptions {
                    // Ignore duplicate apache license files which breaks the AS build
                    exclude 'META-INF/ASL2.0'
                    exclude 'META-INF/LICENSE'
                }
            }
        }
    }
}
